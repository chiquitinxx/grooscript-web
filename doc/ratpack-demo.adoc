= Using link:index.html[grooscript] gradle plugin with http://ratpack.io[Ratpack]

There is no special integration support for Ratpack applications, just with the link:starting_gradle.html[gradle plugin] you can convert
your groovy files to javascript and use in your browser.

The source code of this demo is available in https://github.com/chiquitinxx/ratpack-grooscript-demo[github]. A similar
application done with http://projects.spring.io/spring-boot/[Spring Boot] is deployed in http://ecosystem-gr8.rhcloud.com/, and
the source code also https://github.com/chiquitinxx/springboot-rest-demo[here]. This is my first application using
Ratpack, any pull request to improve it is more than welcome. I don't use any database, only a little static class
with the frameworks added.

Is a single page, you can add groovy frameworks, and see the list of all the frameworks:

image::img/frameworks.png[Demo]

== Setup

I'm using ratpack gradle plugin, and I have added grooscript gradle plugin in +build.gradle+:

[source,groovy]
[subs="verbatim,attributes"]
--
buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "io.ratpack:ratpack-gradle:0.9.18"
    classpath "org.grooscript:grooscript-gradle-plugin:{gradle-plugin-version}"
  }
}

apply plugin: "io.ratpack.ratpack-groovy"
apply plugin: "org.grooscript.conversion"

dependencies {
    ...
    compile 'org.grooscript:grooscript:{grooscript-version}'
    ...
}

task wrapper(type: Wrapper) {
    gradleVersion = '{gradle-version}'
}

grooscript {
    source = ['src/main/groovy/frameworks/FrameworksPresenter.groovy'] //<1>
    destination = 'src/ratpack/public/scripts' //<2>
    classpath = ['src/main/groovy'] //<3>
    includeDependencies = true //<4>
}

run.dependsOn convertThread //<5>
--
<1> The groovy file that will be converted to javascript.
<2> Folder where FrameworksPresenter.js will be saved.
<3> Classpath needed to compile groovy file.
<4> If you use any other groovy class in FrameworksPresenter.groovy, that classes will be converted too, and added to .js file.
<5> While Ratpack application is running in development, any modification done in FrameworksPresenter.groovy, will convert a new .js file.

To play with grooscript, clone the source code and run with `> ./gradlew run`. Now you can modify the file FrameworksPresenter.groovy,
each time you update that file, the .js file will be generated again, and you can reload the page `http://localhost:5050/` to see the changes.

== Hybrid render

One interesting feature of this demo, is the render of the framework list, that is done in the server. But, when a new
framework is added, the same code is used to draw the new framework at the end of the list. Using the grooscript html
builder, you can create html code with usual groovy dsl syntax, and will run in the server and in the client.

In the server `Ratpack.groovy`:

[source,groovy]
--
import static frameworks.FrameworksPresenter

get {
    def list = FrameworksPresenter.htmlFrameworks(FrameworksDb.listFrameworks)
    render groovyMarkupTemplate([frameworks: list], "index.gtpl")
}
--

And in the client `FrameworksPresenter.groovy`:

[source,groovy]
--
import org.grooscript.builder.HtmlBuilder

def buttonAddFrameworkClick() {

    ...
        addFramework(framework, { newFramework ->
            append '#listFrameworks', htmlFramework(newFramework)
        }, { error ->
            putHtml('#validationError', 'Error from server!')
        })
    }
    ...
}

static String htmlFrameworks(List<Framework> frameworks) {
    HtmlBuilder.build {
        ul(id: 'listFrameworks') {
            frameworks.each { framework ->
                yield htmlFramework(framework)
            }
        }
    }
}

static String htmlFramework(Framework framework) {
    HtmlBuilder.build {
        li {
            div(class: 'logo', 'data-anijs': 'if: mouseenter, do: flip animated') {
                if (!framework.hasImage() && framework.githubUrl()) {
                    img src: 'images/github.png'
                } else {
                    img src: framework.hasImage() ? framework.urlImage : 'images/nologo.png'
                }
            }
            a (href: framework.url, framework.name)
        }
    }
}
--

== Conclusion

If you use gradle with your Ratpack application, is very easy add grooscript to your application with the link:starting_gradle.html[gradle plugin].

You can explore grooscript features in link:doc.html[documentation], also download grooscript .js files link:downloads.html[here]

Please, if you have comments, suggestions, problems,... don't hesitate to contact me at grooscript@gmail.com, or open
an issue or feature in http://github.com/chiquitinxx/grooscript/issues?state=open[Github].